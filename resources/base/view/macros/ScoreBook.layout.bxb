layout-macro-def(scoreBook) {
  params {
    param (scoreBook) {
      type (ScoreBook)
      min (Required)
      max (One)
    }
  }
  content {
    section {
      content {
        title-area {
          slot1 {
            text {
              style(Title_M)
              value("#{macro('QUESTION_REVIEW')}:")
            }
          }
        }
        for-each (scoreBook.scores) {
          as (score) {
            paragraph {
              style (Title_S)
              value ("#{macro('QUESTION')}: #{value(score.question.textToDisplay)}")
            }
            cell-area {
              slot1 {
                image {
                  shape (Circle)
                  url {
                    if (score.evaluation == true) {
                      template ("/icons/correct.png")
                    } else {
                      template ("/icons/incorrect.png")
                    }
                  }
                }
              }
              slot2 {
                content {
                  order (PrimarySecondary)
                  primary {
                    if (exists(score.expectedAnswer)) {
                      if (score.evaluation == false) {
                        template ("'#{value(score.answer)}'")
                      } else {
                        template ("'#{value(score.expectedAnswer)}'")
                      }
                    } 
                  }
                }
              }
            }
            if (score.evaluation == false) {
              paragraph {
                style (Detail_L)
                value {
                  template ("#{macro('CORRECT_ANSWER')}: '#{value(score.expectedAnswer)}'[ - #{value(score.question.correctAnswer.explanation)}]")
                }
              }
            }
            paragraph {
              value {
                template ("")
              }
            }
            divider
          }
        }
      }
    }
  }
}

